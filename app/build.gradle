apply plugin: 'com.android.application'

group = 'com.artifex.mupdf'
version = '1.19.0'

dependencies {
	implementation 'androidx.appcompat:appcompat:1.4.1'
	if (file('../lib/build.gradle').isFile())
		api project(':lib')
	else
		api 'com.artifex.mupdf:viewer:1.19.0'
}

android {
	compileSdkVersion 31
	defaultConfig {
		minSdkVersion 16
		targetSdkVersion 31
		versionName '1.19.0'
		versionCode 100
	}

	splits {
		abi {
			enable true
			universalApk true
		}
	}

	bundle {
		abi {
			enableSplit true
		}
	}

	if (project.hasProperty('release_storeFile')) {
		signingConfigs {
			release {
				storeFile file(release_storeFile)
				storePassword release_storePassword
				keyAlias release_keyAlias
				keyPassword release_keyPassword
			}
		}
		buildTypes {
			release {
				signingConfig signingConfigs.release
			}
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}
